plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    compileSdk cfg.sdk.compileVersion

    defaultConfig {
        minSdk cfg.sdk.minVersion
        targetSdk cfg.sdk.targetVersion
        kapt {
            //ERROR : error.NonExistentClass Kotlin In multi module Dagger project
            //https://kotlinlang.org/docs/reference/kapt.html#non-existent-type-correction
            correctErrorTypes = true
        }
        // Desugaring: Required when setting minSdkVersion to 20 or lower
        // multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility cfg.jvm.compatibility.source
        targetCompatibility cfg.jvm.compatibility.target
    }
    kotlinOptions {
        jvmTarget = cfg.jvm.target

        // Enable Coroutines and Flow APIs
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.composeKotlinExt
    }
    // Desugaring: Newer versions of the Android Gradle plugin (starting with 4.0) are able to use desugaring to
    // backport new APIs to older versions of Android.
    // You can read more about it here: https://jakewharton.com/d8-library-desugaring/
    // https://www.rockandnull.com/java-time-android/
    compileOptions {
        coreLibraryDesugaringEnabled true
    }
    namespace 'com.oborodulin.home.accounting'
}
dependencies {
    implementation project(":common")
    implementation project(":data")
    implementation project(":domain")
    implementation project(':presentation')

    implementation project(':metering')
    implementation project(":billing")

    // Logging
    implementation deps.logging.timber
    implementation deps.logging.logger
    // Room
    implementation deps.room.runtime
    kapt deps.room.compiler
    implementation deps.room.ktx
    // Compose
    implementation deps.compose.ui
    implementation deps.compose.material
    implementation deps.compose.iconsCore
    implementation deps.compose.iconsExtended
    implementation deps.compose.uiTooling
    implementation deps.compose.uiPreview
    // Compose preview
    debugImplementation deps.androidx.poolingcontainer
    // Compose Constraint Layout
    implementation deps.androidx.constraintlayoutCompose
    // Hilt
    implementation deps.dagger.hilt
    kapt deps.dagger.compiler
    implementation deps.hilt.navigationCompose
    // Paging
    implementation deps.paging.compose
    // Sweet Toast
    implementation deps.TalhaFaki.composableSweetToast
    // Lottie
    implementation deps.lottie.compose
    // Network
    implementation deps.retrofit2.retrofit
    implementation deps.retrofit2.converterGson
    // Desugaring
    coreLibraryDesugaring deps.tools.desugar

    implementation deps.androidx.coreKtx
    implementation deps.androidx.appcompat
    implementation deps.google.material
    implementation deps.activity.compose
    implementation deps.lifecycle.runtimeKtx
    implementation deps.lifecycle.viewmodelCompose
    implementation deps.lifecycle.runtimeCompose
    implementation deps.google.gson
    implementation deps.navigation.compose
    //debugImplementation deps.compose.uiTooling

    // Testing dependencies
    testImplementation deps.test.junit
    androidTestImplementation deps.test.extJunit
    androidTestImplementation deps.test.espressoCore
    androidTestImplementation deps.compose.uiTestJunit4
}